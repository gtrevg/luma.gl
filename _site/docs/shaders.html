<h1 id="Shaders">Object: Shaders</h1>

<p>An object that contains default shaders that could be used with the <a href="scene.html">Scene</a> class. Only one vertex shader and one fragment
shader are shipped in this object. This is so because we encourage having shaders in separate files and access them in
an asynchronous way by using <code class="highlighter-rouge">Program.fromShaderURIs</code> and other methods available in the Framework. You can set shader strings
into <code class="highlighter-rouge">Shaders.Vertex</code> and <code class="highlighter-rouge">Shaders.Fragment</code>. We provide a default vertex and fragment shader in <code class="highlighter-rouge">Shaders.Vertex.Default</code> and
<code class="highlighter-rouge">Shaders.Fragment.Default</code>. These shaders can also be conveniently used with <code class="highlighter-rouge">Program.fromDefaultShaders(vertexShaderName, fragmentShaderName)</code>.</p>

<h2 id="Shaders:Vertex">Shaders Object: Vertex</h2>

<p>Append in this object vertex shaders to be used with a <a href="scene.html">Scene</a>. We provide <code class="highlighter-rouge">Shaders.Vertex.Default</code> which is the
default shader used in the library. You can find more scene compatible shaders <a href="https://github.com/philogb/philogl/tree/master/shaders">here</a>.
In order to get familiar with the attributes and uniforms used by the <a href="scene.html">Scene</a> we provide the default vertex shader code:</p>

<div class="highlighter-rouge"><pre class="highlight"><code>#define LIGHT_MAX 4
//object attributes
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
attribute vec4 pickingColor;
attribute vec2 texCoord1;
//camera and object matrices
uniform mat4 viewMatrix;
uniform mat4 viewInverseMatrix;
uniform mat4 projectionMatrix;
uniform mat4 viewProjectionMatrix;
//objectMatrix * viewMatrix = worldMatrix
uniform mat4 worldMatrix;
uniform mat4 worldInverseMatrix;
uniform mat4 worldInverseTransposeMatrix;
uniform mat4 objectMatrix;
uniform vec3 cameraPosition;
//lighting configuration
uniform bool enableLights;
uniform vec3 ambientColor;
uniform vec3 directionalColor;
uniform vec3 lightingDirection;
//point lights configuration
uniform vec3 pointLocation[LIGHT_MAX];
uniform vec3 pointColor[LIGHT_MAX];
uniform int numberPoints;
//reflection / refraction configuration
uniform bool useReflection;
//varyings
varying vec3 vReflection;
varying vec4 vColor;
varying vec4 vPickingColor;
varying vec2 vTexCoord;
varying vec4 vNormal;
varying vec3 lightWeighting;

void main(void) {
  vec4 mvPosition = worldMatrix * vec4(position, 1.0);
  vec4 transformedNormal = worldInverseTransposeMatrix * vec4(normal, 1.0);
  //lighting code
  if(!enableLights) {
    lightWeighting = vec3(1.0, 1.0, 1.0);
  } else {
    vec3 plightDirection;
    vec3 pointWeight = vec3(0.0, 0.0, 0.0);
    float directionalLightWeighting = max(dot(transformedNormal.xyz, lightingDirection), 0.0);
    for (int i = 0; i &lt; LIGHT_MAX; i++) {
      if (i &lt; numberPoints) {
        plightDirection = normalize((viewMatrix * vec4(pointLocation[i], 1.0)).xyz - mvPosition.xyz);
        pointWeight += max(dot(transformedNormal.xyz, plightDirection), 0.0) * pointColor[i];
      } else {
        break;
      }
    }

    lightWeighting = ambientColor + (directionalColor * directionalLightWeighting) + pointWeight;
  }
  //refraction / reflection code
  if (useReflection) {
    vReflection = (viewInverseMatrix[3] - (worldMatrix * vec4(position, 1.0))).xyz;
  } else {
    vReflection = vec3(1.0, 1.0, 1.0);
  }
  //pass results to varyings
  vColor = color;
  vPickingColor = pickingColor;
  vTexCoord = texCoord1;
  vNormal = transformedNormal;
  gl_Position = projectionMatrix * worldMatrix * vec4(position, 1.0);
}
</code></pre>
</div>

<h3 id="syntax">Syntax:</h3>

<div class="highlighter-rouge"><pre class="highlight"><code>LumaGL.Shaders.Vertex.MyName = shaderCode;
</code></pre>
</div>

<h3 id="examples">Examples:</h3>

<p>See the example on how to extend the fragment shader object below.</p>

<h2 id="Shaders:Fragment">Shaders Object: Fragment</h2>

<p>Append in this object fragment shaders to be used with a <a href="scene.html">Scene</a>. We provide <code class="highlighter-rouge">Shaders.Fragment.Default</code> which is the
default shader used in the library. You can find more scene compatible shaders <a href="https://github.com/philogb/philogl/tree/master/shaders">here</a>.
In order to get familiar with the attributes and uniforms used by the <a href="scene.html">Scene</a> we provide the default fragment shader code:</p>

<div class="highlighter-rouge"><pre class="highlight"><code>#ifdef GL_ES
precision highp float;
#endif
//varyings
varying vec4 vColor;
varying vec4 vPickingColor;
varying vec2 vTexCoord;
varying vec3 lightWeighting;
varying vec3 vReflection;
varying vec4 vNormal;
//texture configs
uniform bool hasTexture1;
uniform sampler2D sampler1;
uniform bool hasTextureCube1;
uniform samplerCube samplerCube1;
//picking configs
uniform bool enablePicking;
uniform bool hasPickingColors;
uniform vec3 pickColor;
//reflection / refraction configs
uniform float reflection;
uniform float refraction;
//fog configuration
uniform bool hasFog;
uniform vec3 fogColor;
uniform float fogNear;
uniform float fogFar;

void main(){
  //set color from texture
  if (!hasTexture1) {
    gl_FragColor = vec4(vColor.rgb * lightWeighting, vColor.a);
  } else {
    gl_FragColor = vec4(texture2D(sampler1, vec2(vTexCoord.s, vTexCoord.t)).rgb * lightWeighting, 1.0);
  }
  //has cube texture then apply reflection
 if (hasTextureCube1) {
   vec3 nReflection = normalize(vReflection);
   vec3 reflectionValue;
   if (refraction &gt; 0.0) {
    reflectionValue = refract(nReflection, vNormal.xyz, refraction);
   } else {
    reflectionValue = -reflect(nReflection, vNormal.xyz);
   }
   vec4 cubeColor = textureCube(samplerCube1, vec3(-reflectionValue.x, -reflectionValue.y, reflectionValue.z));
   gl_FragColor = vec4(mix(gl_FragColor.xyz, cubeColor.xyz, reflection), 1.0);
 }
  //set picking
  if (enablePicking) {
    if (hasPickingColors) {
      gl_FragColor = vPickingColor;
    } else {
      gl_FragColor = vec4(pickColor, 1.0);
    }
  }
  //handle fog
  if (hasFog) {
    float depth = gl_FragCoord.z / gl_FragCoord.w;
    float fogFactor = smoothstep(fogNear, fogFar, depth);
    gl_FragColor = mix(gl_FragColor, vec4(fogColor, gl_FragColor.w), fogFactor);
  }
}
</code></pre>
</div>

<h3 id="syntax-1">Syntax:</h3>

<div class="highlighter-rouge"><pre class="highlight"><code>LumaGL.Shaders.Fragment.MyName = shaderCode;
</code></pre>
</div>

<h3 id="examples-1">Examples:</h3>

<p>Extending the Fragment Shader object to use a blending uniform. You can see the entire example in <a href="http://philogb.github.com/philogl/LumaGL/examples/lessons/8/">lesson 8</a>.</p>

<figure class="highlight"><pre><code class="language-js" data-lang="js"><span class="c1">//Add Blend Fragment Shader</span>
<span class="nx">LumaGL</span><span class="p">.</span><span class="nx">Shaders</span><span class="p">.</span><span class="nx">Fragment</span><span class="p">.</span><span class="nx">Blend</span> <span class="o">=</span> <span class="p">[</span>

    <span class="s2">"#ifdef GL_ES"</span><span class="p">,</span>
    <span class="s2">"precision highp float;"</span><span class="p">,</span>
    <span class="s2">"#endif"</span><span class="p">,</span>

    <span class="s2">"varying vec4 vColor;"</span><span class="p">,</span>
    <span class="s2">"varying vec2 vTexCoord;"</span><span class="p">,</span>
    <span class="s2">"varying vec3 lightWeighting;"</span><span class="p">,</span>

    <span class="s2">"uniform bool hasTexture1;"</span><span class="p">,</span>
    <span class="s2">"uniform sampler2D sampler1;"</span><span class="p">,</span>
    <span class="s2">"uniform float alpha;"</span><span class="p">,</span>

    <span class="s2">"void main(){"</span><span class="p">,</span>

      <span class="s2">"if (hasTexture1) {"</span><span class="p">,</span>

        <span class="s2">"gl_FragColor = vec4(texture2D(sampler1, vec2(vTexCoord.s, vTexCoord.t)).rgb * lightWeighting, alpha);"</span><span class="p">,</span>

      <span class="s2">"}"</span><span class="p">,</span>

    <span class="s2">"}"</span>

<span class="p">].</span><span class="nx">join</span><span class="p">(</span><span class="s2">"\n"</span><span class="p">);</span></code></pre></figure>

